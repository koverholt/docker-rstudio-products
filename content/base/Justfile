set positional-arguments

BUILDX_PATH := ""

IMAGE_PREFIX := "rstudio/"
PRODUCT := "content-base"
IMAGE_OS := "ubuntu2204"

R_VERSION := "3.6.3"

PYTHON_VERSION := "3.9.5"

QUARTO_VERSION := "1.3.340"

DEFAULT_TAG := IMAGE_PREFIX + PRODUCT + ":r" + R_VERSION + "-py" + PYTHON_VERSION + "-" + IMAGE_OS

# Build content base image - just build ubuntu2204 3.6.3 3.9.5 rstudio/content-base:r3.6.3-py3.9.5-bionic
build OS=IMAGE_OS _R_VERSION=R_VERSION _PYTHON_VERSION=PYTHON_VERSION +TAGS="":
  #!/usr/bin/env bash
  set -euxo pipefail
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=/tmp/.buildx-cache --cache-to=type=local,dest=/tmp/.buildx-cache"
  fi

  tag_array=()
  if [[ "{{TAGS}}" == "" ]]; then
      read -a OS_ALIASES <<< $(just -f {{ justfile_directory() }}/../../ci.Justfile _get-os-alias {{OS}})
      for os_name in ${OS_ALIASES[@]};
      do
        tag_array+=(
          "-t" "rstudio/{{IMAGE_PREFIX}}{{PRODUCT}}:r{{_R_VERSION}}-py{{_PYTHON_VERSION}}-${os_name}"
        )
      done
  else
      for TAG in {{TAGS}}
      do
        tag_array+=("-t" $TAG)
      done
  fi

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg R_VERSION="{{ _R_VERSION }}" \
    --build-arg PYTHON_VERSION="{{ _PYTHON_VERSION }}" \
    --build-arg QUARTO_VERSION="{{ QUARTO_VERSION }}" \
    --file="Dockerfile.{{ OS }}" {{ justfile_directory() }}
