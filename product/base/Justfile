set positional-arguments

BUILDX_PATH := ""

PRODUCT := "product-base"
IMAGE_OS := "ubuntu1804"

IMAGE_REGISTRY := "rstudio"

R_VERSION := "4.1.0"
R_VERSION_ALT := "3.6.2"

PYTHON_VERSION := "3.9.5"
PYTHON_VERSION_ALT := "3.8.10"

TINI_VERSION := "0.19.0"
QUARTO_VERSION := "1.1.251"

_make-default-tag OS=IMAGE_OS:
  echo "{{IMAGE_REGISTRY}}/{{PRODUCT}}:{{OS}}-r{{R_VERSION}}_{{R_VERSION_ALT}}-py{{PYTHON_VERSION}}_{{PYTHON_VERSION_ALT}}"

# Build base image - just build ubuntu1804 rstudio/product-base:ubuntu1804
build OS=IMAGE_OS *TAGS="":
  #!/usr/bin/env bash
  set -euxo pipefail
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=/tmp/.buildx-cache --cache-to=type=local,dest=/tmp/.buildx-cache"
  fi

  if [[ "{{TAGS}}" == "" ]]; then
    raw_tag_array=($(just _make-default-tag {{OS}}))
  else
    raw_tag_array=("{{TAGS}}")
  fi

  tag_array=()
  for TAG in {{TAGS}}
  do
    tag_array+=("-t" $TAG)
  done

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg R_VERSION="{{ R_VERSION }}" \
    --build-arg R_VERSION_ALT="{{ R_VERSION_ALT }}" \
    --build-arg PYTHON_VERSION="{{ PYTHON_VERSION }}" \
    --build-arg PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
    --build-arg TINI_VERSION="{{ TINI_VERSION }}" \
    --build-arg QUARTO_VERSION="{{ QUARTO_VERSION }}" \
    --file=./Dockerfile.$(just -f ../../Justfile _parse-os {{OS}}) .

# Test base image - just test rstudio/product-base:ubuntu1804
test TAG=`just _make-default-tag` CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  IMAGE_NAME="{{ TAG }}" \
  R_VERSION="{{ R_VERSION }}" \
  R_VERSION_ALT="{{ R_VERSION_ALT }}" \
  PYTHON_VERSION="{{ PYTHON_VERSION }}" \
  PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
  QUARTO_VERSION="{{ QUARTO_VERSION }}" \
  TINI_VERSION="{{ TINI_VERSION }}" \
  OS="{{ IMAGE_OS }}" \
  docker-compose -f ./docker-compose.test.yml run sut {{ CMD }}

# Test base image interactively - just test-i rstudio/product-base:ubuntu1804
test-i TAG=`just _make-default-tag`:
  just test {{ TAG }} bash
