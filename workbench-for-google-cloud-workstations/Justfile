set positional-arguments

BUILDX_PATH := ""

IMAGE_PREFIX := "rstudio-"
PRODUCT := "workbench"
IMAGE_OS := "ubuntu2004"

RSW_VERSION := "2023.03.1+446.pro1"
RSW_LICENSE := ""
RSW_DOWNLOAD_URL := "https://download2.rstudio.org/server/bionic/amd64"
RSW_LICENSE_SERVER := ""
DRIVERS_VERSION := "2023.05.0"

R_VERSION := "4.2.3"
R_VERSION_ALT := "4.1.3"

PYTHON_VERSION := "3.10.11"
PYTHON_VERSION_ALT := "3.9.16"
PYTHON_VERSION_JUPYTER := "3.10.11"

QUARTO_VERSION := "1.3.324"

_make-default-tag:
  echo "{{IMAGE_PREFIX}}{{PRODUCT}}:$(just -f ../Justfile _get-tag-safe-version {{RSW_VERSION}})"

# Build Workbench image - just build 2022.07.2+576.pro12 rstudio/rstudio-workbench-gcw:2022.07.2
build *TAGS="":
  #!/usr/bin/env bash
  set -euxo pipefail
  CACHE_PATH=${GITHUB_WORKSPACE:-/tmp}
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=${CACHE_PATH}/.buildx-cache --cache-to=type=local,dest=${CACHE_PATH}/.buildx-cache-new,mode=max"
  fi

  if [[ "{{TAGS}}" == "" ]]; then
    raw_tag_array=($(just IMAGE_PREFIX="{{IMAGE_PREFIX}}" RSW_VERSION="{{RSW_VERSION}}" _make-default-tag))
  else
    raw_tag_array=("{{TAGS}}")
  fi

  tag_array=()
  for tag in ${raw_tag_array[@]};
  do
    tag_array+=("-t" $tag)
  done

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg RSW_VERSION="{{ RSW_VERSION }}" \
    --build-arg R_VERSION="{{ R_VERSION }}" \
    --build-arg R_VERSION_ALT="{{ R_VERSION_ALT }}" \
    --build-arg PYTHON_VERSION="{{ PYTHON_VERSION }}" \
    --build-arg PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
    --build-arg PYTHON_VERSION_JUPYTER="{{ PYTHON_VERSION_JUPYTER }}" \
    --build-arg QUARTO_VERSION="{{ QUARTO_VERSION }}" \
    --build-arg DRIVERS_VERSION="{{ DRIVERS_VERSION }}" \
    --build-arg RSW_DOWNLOAD_URL="{{ RSW_DOWNLOAD_URL }}" \
    --file=./Dockerfile .

  echo ${raw_tag_array[@]}

# Test Workbench image - just test rstudio/rstudio-workbench:ubuntu1804-2022.07.2-576.pro12 2022.07.2+576.pro12
test TAG=`just _make-default-tag` CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  RSW_VERSION_CLEAN=$(sed "s/daily-/daily+/" <<<{{RSW_VERSION}})
  IMAGE_NAME="{{ TAG }}" \
  RSW_VERSION="$RSW_VERSION_CLEAN" \
  RSW_LICENSE="{{ RSW_LICENSE }}" \
  RSW_LICENSE_SERVER="{{ RSW_LICENSE_SERVER }}" \
  DRIVERS_VERSION="{{ DRIVERS_VERSION }}" \
  R_VERSION="{{ R_VERSION }}" \
  R_VERSION_ALT="{{ R_VERSION_ALT }}" \
  PYTHON_VERSION="{{ PYTHON_VERSION }}" \
  PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
  PYTHON_VERSION_JUPYTER="{{ PYTHON_VERSION_JUPYTER }}" \
  QUARTO_VERSION="{{ QUARTO_VERSION }}" \
  docker-compose -f ./docker-compose.test.yml run sut {{ CMD }}

# Test Workbench image interactively - just test-i rstudio/rstudio-workbench:ubuntu1804-2022.07.2-576.pro12 2022.07.2+576.pro12
test-i TAG=`just _make-default-tag`:
  just test {{ TAG }} bash

# Run Workbench - just RSW_LICENSE="<RSW license>" run rstudio/r-session-complete:ubuntu1804-2022.07.2-576.pro12
run TAG=`just _make-default-tag` CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  if [ -z "{{ RSW_LICENSE }}" ] && [ -z "{{ RSW_LICENSE_SERVER }}" ]; then
    echo "Please set RSW_LICENSE or RSW_LICENSE_SERVER before running."
    exit 1
  fi

  docker run -it --privileged \
    ${volume_opts[@]} \
    -p 8787:80 \
    -e RSW_LICENSE="{{ RSW_LICENSE }}" \
    -e RSW_LICENSE_SERVER="{{ RSW_LICENSE_SERVER }}" \
    "{{ TAG }}" {{ CMD }}
