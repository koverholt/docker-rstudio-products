set positional-arguments

BUILDX_PATH := ""

IMAGE_PREFIX := "rstudio-"
PRODUCT := "workbench"
IMAGE_OS := "ubuntu2204"

RSW_VERSION := "2023.12.0+369.pro3"
RSW_LICENSE := ""

R_VERSION := "4.2.3"
R_VERSION_ALT := "4.1.3"

PYTHON_VERSION := "3.9.17"
PYTHON_VERSION_ALT := "3.8.17"

_make-default-tag OS=IMAGE_OS:
  echo "{{IMAGE_PREFIX}}{{PRODUCT}}:{{OS}}-$(just -f ../Justfile _get-tag-safe-version {{RSW_VERSION}})"

# Build Workbench for Azure ML image - just build ubuntu2204 2022.07.2+576.pro12 rstudio/rstudio-workbench:ubuntu2204-2022.07.2-576.pro12
build OS=IMAGE_OS VERSION=RSW_VERSION *TAGS="":
  #!/usr/bin/env bash
  set -euxo pipefail
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=/tmp/.buildx-cache --cache-to=type=local,dest=/tmp/.buildx-cache"
  fi

  if [[ "{{TAGS}}" == "" ]]; then
    raw_tag_array=($(just _make-default-tag {{OS}}))
  else
    raw_tag_array=("{{TAGS}}")
  fi

  tag_array=()
  for tag in $raw_tag_array
  do
    tag_array+=("-t" $tag)
  done

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg RSW_VERSION="{{ VERSION }}" \
    --build-arg R_VERSION="{{ R_VERSION }}" \
    --build-arg R_VERSION_ALT="{{ R_VERSION_ALT }}" \
    --build-arg PYTHON_VERSION="{{ PYTHON_VERSION }}" \
    --build-arg PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
    --file=./Dockerfile.$(just -f ../Justfile _parse-os {{OS}}) .

# Test Workbench for Azure ML image - just test rstudio/rstudio-workbench:ubuntu2204-2022.07.2-576.pro12 2022.07.2+576.pro12
test TAG=`just _make-default-tag` VERSION=RSW_VERSION CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  IMAGE_NAME="{{ TAG }}" \
  RSW_VERSION="{{ VERSION }}" \
  R_VERSION="{{ R_VERSION }}" \
  R_VERSION_ALT="{{ R_VERSION_ALT }}" \
  PYTHON_VERSION="{{ PYTHON_VERSION }}" \
  PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
  docker-compose -f ./docker-compose.test.yml run sut {{ CMD }}

# Test Workbench for Azure ML image interactively - just test-i rstudio/rstudio-workbench:ubuntu2204-2022.07.2-576.pro12 2022.07.2+576.pro12
test-i TAG=`just _make-default-tag` VERSION=RSW_VERSION:
  just test {{ TAG }} {{ VERSION }} bash

# Run Workbench for Azure ML - just RSW_LICENSE="<RSW license>" run rstudio/r-session-complete:ubuntu2204-2022.07.2-576.pro12
run TAG=`just _make-default-tag` CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  if [ -z "{{ RSW_LICENSE }}" ]; then
    echo "Please set RSW_LICENSE to a valid RStudio Workbench license before running."
    exit 1
  fi
  docker run -it --privileged \
    -p 8787:8787 \
    -e RSW_LICENSE="{{ RSW_LICENSE }}" \
    "{{ TAG }}" {{ CMD }}
